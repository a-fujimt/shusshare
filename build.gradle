plugins {
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'application'
}

group = 'net.akichil'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

mainClassName = 'net.akichil.shusshare.ShusshareApplication'
distTar.enabled = false

distZip {
	archivesBaseName = 'shusshare'
	archiveVersion = ''
}


configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.2'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time'
	implementation 'org.mariadb.jdbc:mariadb-java-client'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.dbunit:dbunit:2.7.3'
	testImplementation 'com.github.springtestdbunit:spring-test-dbunit:1.3.0'
	testImplementation 'com.h2database:h2:2.1.214'
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
	dependsOn test // tests are required to run before generating the report

	reports {
		xml.required = true
		csv.required = false
		html.required = true
	}

	// カバレッジ集計の対象外を設定
	afterEvaluate {
		classDirectories.setFrom(fileTree(
			dir: 'build/classes/java/main/',
			excludes: [ '**/entity/*' ]
		))
	}
}

bootRun {
	sourceResources sourceSets.main
}
